" autoload/vimio/shapes.vim
" ----------------
" Graphical template system control module.
" Used to switch graphic sets, graphic indexes, load template files, and call 
" graphic generation functions.
"
" Contents:
" - vimio#shapes#switch_lev1_index(direction)
" - vimio#shapes#switch_lev2_index(direction)
" - vimio#shapes#update_lev2_info()
" - vimio#shapes#load_and_use_custom_drawset_funcs(func_name,indexes,index,file_name)
" - vimio#shapes#switch_define_graph_set(is_show)
" - vimio#shapes#get_default_graph_functions()
" - vimio#shapes#get_all_graph_functions()
" - vimio#shapes#cleanup_shape_subfuncs()

function! vimio#shapes#switch_lev1_index(direction)
    if a:direction == 1
        let g:vimio_config_shapes['set_index'] = (g:vimio_config_shapes['set_index']+1) % len(g:vimio_config_shapes['value'])
    else
        let g:vimio_config_shapes['set_index'] = (g:vimio_config_shapes['set_index']-1 + len(g:vimio_config_shapes['value'])) % len(g:vimio_config_shapes['value'])
    endif

    call vimio#shapes#update_lev2_info()
    call vimio#popup#update_block()
endfunction

function! vimio#shapes#switch_lev2_index(direction)
    if !exists('g:vimio_config_shapes')
       call vimio#shapes#switch_define_graph_set(0)
    endif

    let step = g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['step'][g:vimio_state_switch_lev2_step_index] * a:direction

    " If index is an array, the limit value is the first element of the array, otherwise it is the number of elements in value
    if type(g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['index']) == type([])
        let size_count = g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['index'][0]
        if a:direction == 1
            let g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['index'][1] = (g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['index'][1]+step) % size_count
        elseif a:direction == -1
            let g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['index'][1] = (g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['index'][1]+step+size_count) % size_count

        endif
    else
        let size_count = len(g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['value'])
        if a:direction == 1
            let g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['index'] = (g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['index']+step) % size_count
        elseif a:direction == -1
            let g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['index'] = (g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['index']+step+size_count) % size_count
        endif
    endif

    call vimio#shapes#update_lev2_info()
    call vimio#popup#update_block()
endfunction

function! vimio#shapes#get_default_graph_functions() abort
  return [
        \ ['Vimio__DefineSmartDrawShapesBasic', [0, [60, 0], [60, 0], [60, 0], [60, 0], [600, 0], [600, 0], 0, 0, [600, 0], 0, [40, 0], 0, 0, 0], 0, 'basic.vim'],
        \ ['Vimio__DefineSmartDrawShapesFiglet', [0, 0, 0, 0, 0, 0], 0, 'figlet.vim'],
        \ ['Vimio__DefineSmartDrawShapesLed', [0], 0, 'led.vim'],
        \ ['Vimio__DefineSmartDrawShapesanimal', [0], 0, 'animal.vim'],
        \ ]
endfunction

function! vimio#shapes#get_all_graph_functions() abort
  let default = vimio#shapes#get_default_graph_functions()
  let user = get(g:, 'vimio_user_shapes_define_graph_functions', [])
  return default + user
endfunction


" 'index': Function Set Index
" '[0, 0, 0]': Index of subcategories within each major category of functions
" 0: Index of lev1 categories of functions
" :TODO: The array in the back can be completely decoupled from the specific definition file, and the number of 
"       elements in the array should be automatically created based on the content in the file.
" This is not set to -1 because there are many basic graphic groups, 
" and we are worried about the startup speed, so it is set to the last group.
let g:vimio_shapes_define_graph_functions = {
    \ 'index': 1,
    \ 'value': vimio#shapes#get_all_graph_functions()
    \ }

" If lev2_index is an array [300, 124],Prove that the current graph is dynamically
"    generated by a function, and the array elements cannot be directly accessed, 
"    but the function needs to be called.
" The first value is the limit of the function that generates the graph, and 
"   the second value is the current index.
" The logic of function parameters
" If step is 1, then the function only needs one parameter.
"            2, So the function needs two parameters (width and height).
function! vimio#shapes#update_lev2_info()
    " Update the contents of clip register
    let lev2_index = g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['index']
    let step_arr = g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['step']
    if type(lev2_index) == type([])
        let func_name = g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['value']

        if step_arr[1] == 1
            let func_param = lev2_index[1]
            let @+ = join(call(func_name, [func_param]), "\n")
        else
            let func_param_row = lev2_index[1] / step_arr[1] + 1
            let func_param_col = lev2_index[1] % step_arr[1]
            let @+ = join(call(func_name, [func_param_row, func_param_col]), "\n")
        endif
    else
        let @+ = join(g:vimio_config_shapes['value'][g:vimio_config_shapes['set_index']]['value'][lev2_index], "\n")
    endif
endfunction


function! vimio#shapes#cleanup_shape_subfuncs() abort
    if exists('g:vimio_state_shapes_sub_funcs') && !empty(g:vimio_state_shapes_sub_funcs)
        for fname in g:vimio_state_shapes_sub_funcs
            " exists('g:var')     Check if global variable g:var exists
            " exists(':Cmd')      Check if Ex command :Cmd exists
            " exists('*Func')     Check if function Func() exists
            " exists('#Group')    Check if autocommand group Group exists
            " exists('##Event')   Check if event (e.g. BufEnter) exists
            if type(fname) == type('') && exists('*' . fname)
                execute 'delfunction!' fname
            endif
        endfor
        let g:vimio_state_shapes_sub_funcs = []
    endif
endfunction


function! vimio#shapes#load_and_use_custom_drawset_funcs(func_name, indexes, index, file_name)
    " First clear all sub-functions in the previous template to release memory
    call vimio#shapes#cleanup_shape_subfuncs()

    " Get the plugin root directory (prefer vimio, fallback to $VIM if not found)
    let plugin_root = vimio#utils#get_plugin_root()
    let plugin_path = plugin_root . '/draw_shaps/' . a:file_name
    let user_dir = get(g:, 'vimio_custom_shapes_dir', '')
    let user_path = user_dir !=# '' ? user_dir . '/' . a:file_name : ''

    if user_path !=# '' && filereadable(user_path)
        let file_path = user_path
    elseif filereadable(plugin_path)
        let file_path = plugin_path
    else
        echohl ErrorMsg
        echom 'vimio: Cannot find draw shape file: ' . a:file_name
        echohl None
        return
    endif

    execute 'source' fnameescape(file_path)

    " Call function
    let result = call(a:func_name, [a:indexes, a:index])

    " Delete function definition after global variables are loaded
    " The purpose of deleting the function here is to save memory (because the 
    " local variables in the function initialization may contain a large number
    " of strings).
    execute 'delfunction!' a:func_name
endfunction

function! vimio#shapes#switch_define_graph_set(is_show)
    " " Record start time
    " let l:start_time = reltime()

    " First, record the index of all lev2.
    if exists('g:vimio_config_shapes')
        let old_index = g:vimio_shapes_define_graph_functions['index']
        for i in range(len(g:vimio_shapes_define_graph_functions['value'][old_index][1]))
            let g:vimio_shapes_define_graph_functions['value'][old_index][1][i] = copy(g:vimio_config_shapes['value'][i]['index'])
        endfor
        
        " Record the index of the lev1 category
        let g:vimio_shapes_define_graph_functions['value'][old_index][2] = g:vimio_config_shapes['set_index']
    endif

    let g:vimio_shapes_define_graph_functions['index'] = (g:vimio_shapes_define_graph_functions['index']+1) % len(g:vimio_shapes_define_graph_functions['value'])
    let index_value = g:vimio_shapes_define_graph_functions['index']

    call vimio#shapes#load_and_use_custom_drawset_funcs(g:vimio_shapes_define_graph_functions['value'][index_value][0], g:vimio_shapes_define_graph_functions['value'][index_value][1], g:vimio_shapes_define_graph_functions['value'][index_value][2], g:vimio_shapes_define_graph_functions['value'][index_value][3])

    call vimio#shapes#update_lev2_info()
    if a:is_show
        call vimio#popup#update_block()
    endif

    " " Record end time
    " let l:end_time = reltime()

    " " Calculate execution time
    " let l:elapsed_time = reltimestr(reltime(l:start_time, l:end_time))

    " " Output execution time
    " echo "Execution time: " . l:elapsed_time
endfunction

